buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
	}
}
plugins {
}

ext.voxellib = project ':voxellib'
ext.revision = 186

apply plugin: 'net.minecraftforge.gradle.tweaker-client'

archivesBaseName = "MineLittlePony"
group = 'com.brohoof.minelp'
version = '1.8'

minecraft {
	version = "1.8"
    mappings = 'snapshot_20151027'
	runDir = 'run'
	replace '@VERSION@',project.version
    tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'
	clientJvmArgs += '-Dmcpenv=true'
}
sourceSets {
	def deps = [
		configurations.forgeGradleMcDeps,
		configurations.forgeGradleMc,
		configurations.provided
	]
	common {
		compileClasspath += files deps
	}
	hdskins {
		compileClasspath += files deps
	}
}
project('forge') {
	apply plugin: 'net.minecraftforge.gradle.forge'
	version = '0'
	minecraft {
		version = '1.8-11.14.3.1543'
		mappings = rootProject.minecraft.mappings
		runDir = rootProject.minecraft.runDir
		replace '@VERSION@',project.version
	}
	dependencies {
		provided rootProject
	}
}
project('LiteLoader'){
	apply plugin: 'mnm.gradle.ap-ide'
	mcMappings = rootProject.minecraft.mappings
}
processResources {
    def props = [
        version:    version,
        mcversion:  minecraft.version,
        revision:   revision
    ]
    inputs.properties props
	from(sourceSets.main.resources.srcDirs) {
		include 'litemod.json'
		expand props
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'litemod.json'
	}
}
allprojects {repositories{
	maven {
		name 'sponge'
		url 'http://repo.spongepowered.org/maven'
	}
}}
dependencies {
	provided project('LiteLoader')
	provided voxellib

	compile sourceSets.common.output
	compile sourceSets.hdskins.output
	hdskinsCompile sourceSets.common.output
}
jar {
	manifest.attributes.remove 'TweakClass'
}
task standaloneJar(type: Jar, dependsOn: [{voxellib.reobfObfJar}, {project('forge').compileJava}]) {
	extension 'litemod'
	from sourceSets*.output
	from {project('forge').sourceSets.main.output}

	// voxellib
    from {voxellib.obfJar.archivePath}

    classifier 'standalone'
}
artifacts {
    archives standaloneJar
}
reobf {
	jar.task.enabled = false
	standaloneJar{}
}

task installMod(type: Copy, dependsOn: build) {
	from standaloneJar.archivePath
	into file("$System.env.APPDATA/.minecraft/mods")
}

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '1.7.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'

ext {
    revision = grgit.log().size()
    hash = grgit.head().abbreviatedId
    if (file('.git/shallow').exists()) {
        // don't clone with --depth
        revision = -1
        hash += " shallow"
    }
}

if (project.release != 'RELEASE') {
    version += "-${project.release}"
}
if (project.release == 'SNAPSHOT') {
    version += "-${project.revision}-${project.hash}"
}

group = project.group
description = project.displayname

sourceSets {
    common {
        // code shared between HDSkins and MineLP mods
        compileClasspath += main.compileClasspath
    }

    main {
        compileClasspath += common.output
    }

    fml {
        compileClasspath += main.compileClasspath
        compileClasspath += common.output
        compileClasspath += main.output
    }
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version + '-' + project.minecraft_version

    runs {
        client {
            workingDirectory file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                hdskins {
                    // Remember to add /bin/default to the Environment configs
                    // hdskins%%C:/.../bin/default
                    source sourceSets.fml
                }
            }
        }
    }
}

targetCompatibility = 1.8
sourceCompatibility = 1.8
compileJava.sourceCompatibility = '1.8'
compileJava.targetCompatibility = '1.8'

dependencies {
    minecraft "net.minecraftforge:forge:" + project.minecraft_version + "-" + project.forge_version
    // use the same version as httpclient
    compile('org.apache.httpcomponents:httpmime:4.3.2') {
        transitive = false
    }
}

manifest {
    attributes(
            "Specification-Title": project.name,
            "Specification-Vendor": project.name,
            "Specification-Version": "${version}",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version} (git-${project.hash})",
            "Implementation-Vendor": project.name,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    )
}

archivesBaseName = "mod-${project.name.toLowerCase()}"

jar {
    classifier 'base'
    extension 'jar'
}

shadowJar {
    dependencies {
        exclude 'META-INF/**'
    }

    relocate 'org.apache.http.entity.mime', 'com.voxelmodpack.repack.org.apache.http.entity.mime'
}

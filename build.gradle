buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'org.ajoberstar.grgit:grgit-gradle:3.1.1'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'eclipse'

ext {
    revision = grgit.log().size()
    hash = grgit.head().abbreviatedId
    if (file('.git/shallow').exists()) {
        // don't clone with --depth
        revision = -1
        hash += " shallow"
    }
}

if (project.release != 'RELEASE') {
    version += "-${project.release}"
}
if (project.release == 'SNAPSHOT') {
    version += "-${project.revision}-${project.hash}"
}

group = project.group
description = project.displayname

sourceSets {
    main {

    }

    // Workaround for broken classloading with forge in 1.13
    kirin {
        java {
            srcDir "../Kirin/src/main/java"
        }
    }

    fml {
        compileClasspath += main.compileClasspath
        compileClasspath += main.output
    }
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version + '-' + project.minecraft_version

    runs {
        client {
            workingDirectory file('run')

            property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'

            mods {
                hdskins {
                    // TODO: Gradle generates broken eclipse configs. Make sure you apply the following changes otherwise the game won't run:
                    //  - (Optional) Remove test/java from the classpath
                    //  - (Optional) Remove test/resources from the classpath
                    //  - Disable "Allow output folders for source folders"
                    //  - Remove the kirin.jar from the classpath
                    //  - Change the run config to use the following string for finding mod classes:
                            // TODO: Remember to add /bin/default to the Environment configs
                            // hdskins%%C:/.../bin/default;hdskins%%C:/.../bin/default
                            // * Duplicated twice because forge is retarded
//                    source sourceSets.main
                    source sourceSets.fml
                }
            }
        }
    }
}

// From the forge MDK: Need this here so eclipse task generates correctly.
targetCompatibility = compileJava.targetCompatibility = 1.8
sourceCompatibility = compileJava.sourceCompatibility = 1.8

repositories {
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {

    minecraft "net.minecraftforge:forge:" + project.minecraft_version + "-" + project.forge_version

    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
    // use the same version as httpclient
    compile('org.apache.httpcomponents:httpmime:4.3.2') {
        transitive = false
    }

    compile "com.github.MineLittlePony:Kirin:master-SNAPSHOT:base"
}

manifest {
    attributes(
        "Implementation-Version": "${version} (git-${project.hash})",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    )
}

compileJava.options.compilerArgs += '-proc:none'

jar {
    classifier 'base'
    extension 'jar'
}

shadowJar {
    dependencies {
        exclude 'META-INF/**'
    }

    relocate 'org.apache.http.entity.mime', 'com.minelittlepony.repack.org.apache.http.entity.mime'
}

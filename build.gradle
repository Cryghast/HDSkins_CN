buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'org.ajoberstar.grgit:grgit-gradle:3.1.1'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'eclipse'

ext {
    revision = grgit.log().size()
    hash = grgit.head().abbreviatedId
    if (file('.git/shallow').exists()) {
        // don't clone with --depth
        revision = -1
        hash += " shallow"
    }
}

if (project.release != 'RELEASE') {
    version += "-${project.release}"
}
if (project.release == 'SNAPSHOT') {
    version += "-${project.revision}-${project.hash}"
}

group = project.group
description = project.displayname

sourceSets {
    fml {
        compileClasspath += main.compileClasspath
        compileClasspath += main.output
    }
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version + '-' + project.minecraft_version

    runs {
        client {
            workingDirectory file('run')

            property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'

            mods {
                hdskins {
                    source sourceSets.fml
                    source sourceSets.main
                }
            }
            ideaModule = "HDSkins.fml"
        }
    }
}

// From the forge MDK: Need this here so eclipse task generates correctly.
targetCompatibility = compileJava.targetCompatibility = 1.8
sourceCompatibility = compileJava.sourceCompatibility = 1.8

repositories {
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {

    minecraft "net.minecraftforge:forge:" + project.minecraft_version + "-" + project.forge_version

    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
    // use the same version as httpclient
    compile('org.apache.httpcomponents:httpmime:4.3.2') {
        transitive = false
    }

    compile fg.deobf("com.github.MineLittlePony:Kirin:master-SNAPSHOT")
}

manifest {
    attributes(
        "Implementation-Version": "${version} (git-${project.hash})",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    )
}

// TODO remove when mixingradle gets updated.
compileJava.options.compilerArgs += '-proc:none'

shadowJar {
    classifier 'universal'
    dependencies {
        exclude 'META-INF/**'
    }

    relocate 'org.apache.http.entity.mime', 'com.minelittlepony.repack.org.apache.http.entity.mime'
}
